'-------------------------------------------------
' The following arrays and variables are dynamically
' generated by the main php code that includes this script
'     arrSel, CTG, blSingleMachine, 
'     strSelection, intChartSize
'-------------------------------------------------

' ==============================================================
Sub Window_OnLoad()
	MakeCharts
End Sub

' ==============================================================
Sub cmbSelections_OnChange()

For Each optSel In cmbSelections.Options
	Set cs = document.getElementById("CS"&optSel.Value)
	If optSel.Selected Then
		cs.style.display = "block"
	 Else
		cs.style.display = "none"
	 End If
	Next
	
 MakeCharts
 
End Sub

' ==============================================================
Sub btnShowSeries_onclick()

	If divSeries.Style.display = "none" Or divSeries.Style.display = "" Then
	  divSeries.Style.display = "block"
	  btnShowSeries.Value = "Hide Series"
	  intChartSize = 60
	 Else
	  divSeries.Style.display = "none"
	  btnShowSeries.Value = "Show Series"
	  intChartSize = 80
	 End If

	MakeCharts

End Sub

' ==============================================================
Sub MakeCharts()

Dim i, m, SCe, SN, SV, SC, SelObjCnt
Dim ChtSpc, c, limit_reached

' Prepare chartspace objects
  ChartSpace1.Clear
	ChartSpace1.ChartLayout = ChartSpace1.Constants.chChartLayoutHorizontal
	ChartSpace1.ChartWrapCount = cmbWrap.Value
  ChartSpace2.Clear
	ChartSpace2.ChartLayout = ChartSpace2.Constants.chChartLayoutHorizontal
	ChartSpace2.ChartWrapCount = cmbWrap.Value
  ChartSpace3.Clear
	ChartSpace3.ChartLayout = ChartSpace3.Constants.chChartLayoutHorizontal
	ChartSpace3.ChartWrapCount = cmbWrap.Value

' Initialise environment
	Set ChtSpc = ChartSpace1
	Set c = ChtSpc.Constants
	limit_reached = vbFalse
	SelObjCnt = 0

	For Each optSel In cmbSelections.Options

		If optSel.Selected Then

			i = optSel.Value
			strSel = arrSel(i)
			arrSer = arrSel(i+1)

			SelObjCnt = SelObjCnt + 1	

		' Check that Chartspace limit is not exceeded
			If SelObjCnt = 17 Then			' Switch to ChartSpace2
				Set ChtSpc = ChartSpace2
				Set c = ChtSpc.Constants
			 ElseIf SelObjCnt = 33 Then	' Switch to ChartSpace3
				Set ChtSpc = ChartSpace3
				Set c = ChtSpc.Constants
			 ElseIf SelObjCnt = 49 Then	' Too many
				limit_reached = vbTrue
				Exit For
			 End If

		  Set Cht = ChtSpc.Charts.Add

		' Check each series
			For j = 0 to Ubound(arrSer) Step 2
			
				Set de = Document.getElementbyID("C"&i&"S"&j)
			
				If de.checked Then
					strSer = arrSer(j)
					arrDat = arrSer(j+1)
					For k = 0 to Ubound(arrDat) Step 2
						dt = arrDat(k)
						If ( dt = "Min" And chkMin.checked ) Or ( dt = "Avg" And chkAvg.checked ) Or _
							 ( dt = "Cnt" And chkCnt.checked ) Or ( dt = "Max" And chkMax.checked ) _
							Then
						    Set SC = Cht.SeriesCollection.Add
						    SC.Caption = strSer & " [" & dt & "]"
						    SC.SetData c.chDimCategories, c.chDataLiteral, CTG
						    SC.SetData c.chDimValues, c.chDataLiteral, arrDat(k+1)
						  End If
					 Next
				 End If
			  Next	' next Series
	
	    ' Set the chart type and format the axes
		    Cht.Type = cmbType.Value
		    Cht.Axes(c.chAxisPositionLeft).HasTitle = vbTrue 
			If blSingleMachine Then
			    Cht.Axes(c.chAxisPositionLeft).Title.Caption = strSel
			  Else
			    Cht.Axes(c.chAxisPositionLeft).Title.Caption = strSelection
			  End If
		    Cht.Axes(c.chAxisPositionLeft).NumberFormat = "#,##0"
			If chkCnt.checked Or ( cmbScale.Value = 0 ) Then
			    Cht.Axes(c.chAxisPositionLeft).Scaling.Minimum = 0
			  ElseIf cmbScale.Value = 100 Then
			    Cht.Axes(c.chAxisPositionLeft).Scaling.Minimum = 0
			    Cht.Axes(c.chAxisPositionLeft).Scaling.Maximum = 100
			  End If
		    Cht.Axes(c.chAxisPositionBottom).TickLabelSpacing = ( UBound(CTG) / 8 ) + 1
		    Cht.Border.Color = "red"
	
	    ' Set legend and position
		    Cht.HasLegend = vbTrue
		    Cht.Legend.Position = c.chLegendPositionBottom
	
	    ' Set title and position
		    Cht.HasTitle = vbTrue
			If blSingleMachine Then
			    Cht.Title.Caption = strSelection
			  Else
			    Cht.Title.Caption = strSel
			  End If
	
		  End If  ' Selection selected 

	  Next	' next Selection

' Calculate the number of rows on display
	Set ChtSpc = ChartSpace1
	nr = Int(ChtSpc.Charts.Count / ChtSpc.ChartWrapCount)
	If ChtSpc.ChartWrapCount * nr < ChtSpc.Charts.Count Then
		nr = nr + 1
	  End If

' Set ChartSpace height to give each row 80% of screen
	ChtSpc.Style.Height = (nr * intChartSize) & "%"

' Calculate the number of rows on display
	Set ChtSpc = ChartSpace2
	nr = Int(ChtSpc.Charts.Count / ChtSpc.ChartWrapCount)
	If ChtSpc.ChartWrapCount * nr < ChtSpc.Charts.Count Then
		nr = nr + 1
	  End If

' Set ChartSpace height to give each row 80% of screen
	ChtSpc.Style.Height = (nr * intChartSize) & "%"

' Calculate the number of rows on display
	Set ChtSpc = ChartSpace3
	nr = Int(ChtSpc.Charts.Count / ChtSpc.ChartWrapCount)
	If ChtSpc.ChartWrapCount * nr < ChtSpc.Charts.Count Then
		nr = nr + 1
	  End If

' Set ChartSpace height to give each row 80% of screen
	ChtSpc.Style.Height = (nr * intChartSize) & "%"

' Display a message if the chartspace limit was reached
	If limit_reached Then
		MsgBox "There are too many Charts selected, some have not been displayed." & vbCrLf & vbCrLf & _
			"This page can only display the first 48 charts." & vbCrLf & _
			"Please check the 'Charts' listbox and modify the selection.", , "Chart Limit Exceeded"
	  End If

End Sub
